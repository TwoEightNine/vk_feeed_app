apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "global.msnthrp.feeed"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "BUILD_TIME", "\"${getDate()}\"")
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "BUILD_TIME", "\"${getDate()}\"")
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    implementation "com.google.code.gson:gson:2.8.2"
    implementation 'com.google.android.material:material:1.1.0-alpha03'
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    implementation "com.squareup.picasso:picasso:2.71828"
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.core:core-ktx:1.1.0-alpha04'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.2-alpha01'

    retrofit()
    dagger()
    reactiveX()
    viewModelAndLiveData()
}

def viewModelAndLiveData() {
    dependencies {
        def lifecycle_version = "2.0.0"

        implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    }
}

def retrofit() {
    dependencies {
        def retrofit_version = "2.3.0"
        def okhttp_version = "3.9.1"

        implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
        implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
        implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
        implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
        implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    }
}

def dagger() {
    dependencies {
        def dagger_version = "2.7"

        kapt "com.google.dagger:dagger-compiler:$dagger_version"
        implementation "com.google.dagger:dagger:$dagger_version"
        compileOnly "javax.annotation:jsr250-api:1.0"
    }
}

def reactiveX() {
    dependencies {

        implementation "io.reactivex.rxjava2:rxjava:2.1.7"
        implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
        implementation 'com.jakewharton.rxbinding:rxbinding:1.0.1'
    }
}

kapt {
    generateStubs = true
}

androidExtensions {
    experimental = true
}

repositories {
    mavenCentral()
}

static def getDate() {
    new Date().format('yyyy/MM/dd')
}
